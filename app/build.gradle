buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'me.tatarka:gradle-retrolambda:3.2.3'
    }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    mavenCentral()
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTimestamp = 'git log -n 1 --format=%at'.execute([], rootDir).text.trim()

def packagename = "ru.ltst.u2020mvp"

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION as String

    defaultConfig {

        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField 'String', 'GIT_SHA', "\"${gitSha}\""
        buildConfigField 'long', 'GIT_TIMESTAMP', "${gitTimestamp}"

        testApplicationId "${packagename}.tests"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file("../distribution/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            storeFile file("../distribution/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-rules.pro')
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        internal {
            applicationId "${packagename}.internal"

        }
        production {
            applicationId "${packagename}"
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        warning 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

// TODO remove eventually: http://b.android.com/162285
configurations {
    internalDebugCompile
}

configurations.all {
    resolutionStrategy {
        force "com.android.support:support-annotations:${SUPPORT_V7_VERSION}"
    }
}

dependencies {
    //support
    compile "com.android.support:support-v4:${SUPPORT_V4_VERSION}"
    compile "com.android.support:support-annotations:${SUPPORT_V7_VERSION}"
    compile "com.android.support:appcompat-v7:${SUPPORT_V7_VERSION}"
    compile "com.android.support:recyclerview-v7:${SUPPORT_V7_VERSION}"
    compile "com.android.support:cardview-v7:${SUPPORT_V7_VERSION}"
    compile 'com.android.support:design:23.1.0'
    //dagger
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    //okhttp
    compile 'com.squareup.okhttp3:okhttp:3.1.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.1.1'
    //picasso
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.1'
    //retrofit2
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta4'
    compile 'com.squareup.retrofit2:converter-moshi:2.0.0-beta4'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0-beta4'
    internalDebugCompile 'com.squareup.retrofit2:retrofit-mock:2.0.0-beta4'
    //butterknife
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'
    //timber
    compile 'com.jakewharton.timber:timber:4.0.1'
    //byteunits
    compile 'com.jakewharton.byteunits:byteunits:0.9.1'
    //rx
    compile 'com.jakewharton.rxbinding:rxbinding:0.3.0'
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'io.reactivex:rxandroid:1.1.0'
    //threetenabp
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.2'
    //rx-preferences
    compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.0'

    //-----flawors-----
    //--internal debug--
    internalDebugCompile 'com.jakewharton.madge:madge:1.1.3'
    internalDebugCompile 'com.jakewharton.scalpel:scalpel:1.1.2'
    internalDebugCompile 'com.jakewharton:process-phoenix:1.0.2'
    //--internal--
    internalCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    internalCompile 'com.mattprecious.telescope:telescope:1.5.0@aar'
    //--production--
    productionCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

    //-----tests-----
    // Espresso 2 Dependencies
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.0'
//    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.0') {
//        exclude module: 'support-annotations'
//    }
}

// change apk name
android.applicationVariants.all { variant ->
    for (output in variant.outputs) {
        def outputFile = output.outputFile
        if (outputFile != null && outputFile.name.endsWith('.apk')) {
            def fileName = "u2020-mvp-${output.name}.apk"
            output.outputFile = new File(outputFile.parent, fileName)
        }
    }
}

// print build finish time
gradle.buildFinished { buildResult ->
    def buildFinishDate = new Date()
    def formattedDate = buildFinishDate.format('yyyy-MM-dd HH:mm:ss')
    println "Build finished: ${formattedDate}"
}
